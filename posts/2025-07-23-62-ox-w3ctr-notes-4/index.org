#+title: ox-w3ctr å¼€å‘ç¬”è®° (4)
#+subtitle: å¯¼å‡º headline å’Œ section
#+date: [2025-07-06 Sun 21:15]--[2025-07-23 Wed 11:41]
#+filetags: orgmode
#+description: æœ¬æ–‡æ˜¯ ox-w3ctr å¼€å‘ç¬”è®°çš„ç¬¬å››ç¯‡ï¼Œä»‹ç»äº† headline å’Œ section çš„å¯¼å‡ºå®ç°

#+HTML_HEAD_EXTRA: <style> .name { color: red; } </style>

# [[https://www.pixiv.net/artworks/63740368][file:dev/p1.jpg]]

æˆ‘åœ¨ [2025-07-06 Sun] å¼€å§‹å†™æœ¬ç³»åˆ—ç¬”è®°çš„ç¬¬å››ç¯‡ã€‚å°±åƒä¸Šä¸€ç¯‡è¯´çš„é‚£æ ·ï¼Œç¬¬ä¸‰ç¯‡æœ¬æ¥æ˜¯å‡†å¤‡è®°å½• template å’Œ inner-template å¯¼å‡ºçš„ï¼Œä½†æ˜¯å®ƒçš„å¯¼å‡ºä¾èµ– timestamp çš„å¯¼å‡ºï¼Œå› æ­¤ç¬¬ä¸‰ç¯‡å˜æˆäº†è®°å½•æ—¶é—´æˆ³çš„å¯¼å‡ºã€‚

ç°åœ¨ï¼Œæˆ‘æ€»ç®—èƒ½å¤Ÿå›æ¥è®°å½• template çš„å¯¼å‡ºå®ç°äº†ï¼Œä¸è¿‡ headline ä¸ table of contents(TOC) å…³ç³»å¯†åˆ‡ï¼Œè¿˜æ˜¯å…ˆä» headline å’Œ section å¼€å§‹å§ã€‚æ—¢ç„¶è¿™ä¸ªæœˆè¿˜ä»€ä¹ˆä¹Ÿæ²¡å‘ï¼Œå°±å…ˆæŠŠå†™å®Œçš„ headline å‘äº†ç®—äº†ã€‚

è·ç¦»æœ¬æ–‡æœ€è¿‘çš„ commit æ˜¯ [[https://github.com/include-yy/ox-w3ctr/commit/91797b9f7c089d82470b075abadac57b8e49acc7][91797b9]]ï¼Œæœ¬æ–‡ä½¿ç”¨çš„ Emacs ä¸º Emacs 31.0.50 (build 20, x86_64-w64-mingw32) of 2025-07-21 (commit [[https://github.com/emacs-mirror/emacs/commit/5485bda52399a23d95ab593dcb748975ee0654d0][5485bda]])ã€‚

* section

headline å’Œ section æ˜¯ Org-mode è¯­æ³•ä¸­æœ€å¤§çš„ä¸¤ä¸ªå…ƒç´ ã€‚headline å¹¶ä¸ä»…ä»…æ˜¯ä¸€ä¸ª =<hN>= æ ‡ç­¾ï¼Œå®ƒè¿˜åŒ…å«æ ‡é¢˜åé¢çš„å†…å®¹ï¼ˆå³ sectionï¼‰ï¼Œç›´åˆ°é‡åˆ°å¦ä¸€ä¸ªåŒçº§æ ‡é¢˜ã€‚åœ¨ HTML ä¸­çš„ =<section>= æ ‡ç­¾åŸºæœ¬ä¸Šèƒ½å¯¹åº”äº Org-mode çš„ headline åŠ ä¸Šå®ƒçš„ section å†…å®¹ã€‚

åœ¨ ox-html ä¸­ï¼Œsection çš„å¯¼å‡ºç”± =org-html-section= å®Œæˆï¼š

#+begin_src elisp
  (defun org-html-section (section contents info)
    "Transcode a SECTION element from Org to HTML.
  CONTENTS holds the contents of the section.  INFO is a plist
  holding contextual information."
    (let ((parent (org-element-lineage section 'headline)))
      ;; Before first headline: no container, just return CONTENTS.
      (if (not parent) contents
        ;; Get div's class and id references.
        (let* ((class-num (+ (org-export-get-relative-level parent info)
  			   (1- (plist-get info :html-toplevel-hlevel))))
  	     (section-number
  	      (and (org-export-numbered-headline-p parent info)
  		   (mapconcat
  		    #'number-to-string
  		    (org-export-get-headline-number parent info) "-"))))
          ;; Build return value.
  	(format "<div class=\"outline-text-%d\" id=\"text-%s\">\n%s</div>\n"
  		class-num
  		(or (org-element-property :CUSTOM_ID parent)
  		    section-number
  		    (org-export-get-reference parent info))
  		(or contents ""))))))
#+end_src

æˆ‘ä»¬å¯ä»¥æ³¨æ„åˆ° =org-html-section= ä½¿ç”¨ =org-element-lineage= æ¥æ‰¾åˆ° section çš„åˆ—ç¥–åˆ—å®—ï¼Œå¦‚æœæ²¡æœ‰æ‰¾åˆ°åˆ™è¯´æ˜å®ƒçš„å‰é¢æ²¡æœ‰ headlineï¼Œè¿™åœ¨ Org-mode ä¸­ä¹Ÿå«åš zeroth sectionã€‚å¯¹äºæ™®é€šçš„ sectionï¼Œâ€‹=org-html-section= ä¸ºå®ƒç”Ÿæˆäº†ä¸€ä¸ª =div= wrapperï¼Œå¹¶æ·»åŠ äº†å¿…è¦çš„ç±»å’Œ idã€‚

å½“ç„¶ï¼Œox-w3ctr ä¸»è¦å—åˆ°äº† W3CTR å’Œ ReSpec çš„å½±å“ï¼Œéƒ¨åˆ† W3C æ ‡å‡†æ–‡æ¡£ä¸­çš„ =<section>= æ ¼å¼å¤§è‡´å¦‚ä¸‹ï¼š

#+caption: [[https://www.w3.org/TR/lws-ucs/#introduction][Linked Web Storage Use Cases]]
#+begin_src html
  <section id="introduction">
    <div class="header-wrapper">
      <h2 id="x1-introduction"><bdi class="secno">1. </bdi>Introduction</h2>
      <a class="self-link" href="#introduction" aria-label="Permalink for Section 1."></a>
    </div>
    <p>
      The <abbr title="Linked Web Storage">LWS</abbr> specifications aim
      to enable the development of web applications where data storage,
      entity authentication, access control, and application provider are
      all loosely coupled, as compared to the web of today where these are
      typically all tightly coupled, so changing one requires changing
      all, sometimes at a cost of all past data.
    </p>
    <p>
      This document lists user stories and use-cases for
      the <abbr title="Linked Web Storage">LWS</abbr> specifications, as
      well as requirements identified as necessary to satisfy these use
      cases.
    </p>
  </section>
#+end_src

å¯è§åœ¨ =<section>= ä¸­ï¼Œâ€‹*å†…å®¹*â€‹éƒ¨åˆ†å¹¶æ²¡æœ‰ä»€ä¹ˆå¤šä½™çš„ wrapperï¼Œç±»æˆ–è€… idï¼Œå› æ­¤æˆ‘ä¹Ÿå¯¹ section çš„å¯¼å‡ºåšäº†ç®€åŒ–ï¼š

#+begin_src elisp
  (defvar t--zeroth-section-output nil
    "Internal variable storing zeroth section's HTML output.

  This is used to override the default ox-html behavior where TOC comes
  first, allowing zeroth section's content to appear before the TOC while
  the TOC remains near the beginning of the document.")

  ;; FIXME: consider consider malformed headline(e.g., ** before first *)
  (defun t-section (section contents _info)
    "Transcode a SECTION element from Org to HTML.
  CONTENTS holds the contents of the section.  INFO is a plist
  holding contextual information."
    (declare (ftype (function (t t t) string))
             (important-return-value t))
    ;; normal section
    (if (org-element-lineage section 'headline) contents
      (prog1 nil (setq t--zeroth-section-output contents))))
#+end_src

æ­¤å¤„çš„ =org-w3ctr--zeroth-section-output= å­˜å‚¨äº† zeroth section çš„å¯¼å‡ºç»“æœã€‚å¦‚æœæŒ‰ç…§ ox-html çš„åšæ³•ï¼Œzeroth section å°†æŒ‰ç…§æ­£å¸¸çš„æ–‡æœ¬é¡ºåºä½äºç›®å½•ï¼ˆTable of Contents, TOCï¼‰ä¹‹åï¼Œæˆ‘è®¤ä¸ºè¿™ä¸€éƒ¨åˆ†ä½äº TOC ä¹‹å‰æ›´å¥½ï¼Œå¯ä»¥ä½œä¸ºæ–‡æ¡£çš„å¼•è¨€éƒ¨åˆ†ã€‚â€‹=org-w3ctr--zeroth-section-output= å°†åœ¨ =org-w3ctr-template= è¢«æ’å…¥å¯¼å‡ºç»“æœã€‚ä¸è¿‡æœ¬æ–‡æ²¡æœ‰ä»‹ç» template çš„å¯¼å‡ºï¼Œâ€‹=org-w3ctr--zeroth-section-output= çš„ä½¿ç”¨åº”è¯¥åœ¨ä¸‹ä¸€ç¯‡ã€‚

* headline

åœ¨ ox-html ä¸­ï¼Œheadline çš„å¯¼å‡ºç”± =org-html-headline= è¿™ä¸€å·¨å¤§çš„å‡½æ•°è´Ÿè´£ï¼š

#+begin_src elisp
  (defun org-html-headline (headline contents info)
    "Transcode a HEADLINE element from Org to HTML.
  CONTENTS holds the contents of the headline.  INFO is a plist
  holding contextual information."
    (unless (org-element-property :footnote-section-p headline)
      (let* ((numberedp (org-export-numbered-headline-p headline info))
             (numbers (org-export-get-headline-number headline info))
             (level (+ (org-export-get-relative-level headline info)
                       (1- (plist-get info :html-toplevel-hlevel))))
             (todo (and (plist-get info :with-todo-keywords)
                        (let ((todo (org-element-property :todo-keyword headline)))
                          (and todo (org-export-data todo info)))))
             (todo-type (and todo (org-element-property :todo-type headline)))
             (priority (and (plist-get info :with-priority)
                            (org-element-property :priority headline)))
             (text (org-export-data (org-element-property :title headline) info))
             (tags (and (plist-get info :with-tags)
                        (org-export-get-tags headline info)))
             (full-text (funcall (plist-get info :html-format-headline-function)
                                 todo todo-type priority text tags info))
             (contents (or contents ""))
  	   (id (org-html--reference headline info))
  	   (formatted-text
  	    (if (plist-get info :html-self-link-headlines)
  		(format "<a href=\"#%s\">%s</a>" id full-text)
  	      full-text)))
        (if (org-export-low-level-p headline info)
            ;; This is a deep sub-tree: export it as a list item.
            (let* ((html-type (if numberedp "ol" "ul")))
  	    (concat
  	     (and (org-export-first-sibling-p headline info)
  		  (apply #'format "<%s class=\"org-%s\">\n"
  			 (make-list 2 html-type)))
  	     (org-html-format-list-item
  	      contents (if numberedp 'ordered 'unordered)
  	      nil info nil
  	      (concat (org-html--anchor id nil nil info) formatted-text)) "\n"
  	     (and (org-export-last-sibling-p headline info)
  		  (format "</%s>\n" html-type))))
  	;; Standard headline.  Export it as a section.
          (let ((extra-class
  	       (org-element-property :HTML_CONTAINER_CLASS headline))
  	      (headline-class
  	       (org-element-property :HTML_HEADLINE_CLASS headline))
                (first-content (car (org-element-contents headline))))
            (format "<%s id=\"%s\" class=\"%s\">%s%s</%s>\n"
                    (org-html--container headline info)
                    (format "outline-container-%s" id)
                    (concat (format "outline-%d" level)
                            (and extra-class " ")
                            extra-class)
                    (format "\n<h%d id=\"%s\"%s>%s</h%d>\n"
                            level
                            id
  			  (if (not headline-class) ""
  			    (format " class=\"%s\"" headline-class))
                            (concat
                             (and numberedp
                                  (format
                                   "<span class=\"section-number-%d\">%s</span> "
                                   level
                                   (concat (mapconcat #'number-to-string numbers ".") ".")))
                             formatted-text)
                            level)
                    ;; When there is no section, pretend there is an
                    ;; empty one to get the correct <div
                    ;; class="outline-...> which is needed by
                    ;; `org-info.js'.
                    (if (org-element-type-p first-content 'section) contents
                      (concat (org-html-section first-content "" info) contents))
                    (org-html--container headline info)))))))
#+end_src

ä¸€èˆ¬æ¥è¯´ï¼Œæˆ‘ä¸å¤ªå–œæ¬¢å†™è¶…è¿‡ 40 è¡Œçš„å‡½æ•°ï¼Œä¸»è¦æ˜¯å› ä¸ºä»£ç è¿‡é•¿ä¸å®œç¼–å†™æµ‹è¯•ï¼Œåœ¨ Elisp è¿™ä¸€æ ‡å‡†å¯ä»¥è¿›ä¸€æ­¥å˜ä¸º 30 è¡Œã€‚ä¸‹é¢è®©æˆ‘ä»¬ä¸€æ­¥æ­¥æ‹†åˆ† =org-html-headline= çš„é€»è¾‘ã€‚

** todo, priority å’Œ tags

Org-mode çš„ headline æ”¯æŒ todo, priority å’Œ tag æ ‡è®°ï¼Œä»è€Œå®ç°æ¯”è¾ƒä¸°å¯Œçš„ agenda åŠŸèƒ½ï¼Œä¸è¿‡è¿™äº›è®°å·å¯¹ä»¥å¯¼å‡ºåˆ° HTML ä¸ºç›®çš„çš„åŸºæœ¬ä¸å˜çš„æ–‡æ¡£å€’æ˜¯æ²¡ä»€ä¹ˆå¤§ç”¨ï¼ˆç¬‘ï¼‰ã€‚

åœ¨ ox-html ä¸­ï¼Œè¿™ä¸‰ä¸ªå®ä½“åˆ†åˆ«ç”± =org-html--todo=, =org-html--priority= å’Œ =org-html--tags= å¯¼å‡ºï¼š

#+begin_src elisp
  (defun org-html--todo (todo info)
    (when todo
      (format "<span class=\"%s %s%s\">%s</span>"
  	    (if (member todo org-done-keywords) "done" "todo")
  	    (or (plist-get info :html-todo-kwd-class-prefix) "")
  	    (org-html-fix-class-name todo)
  	    todo)))

  (defun org-html--priority (priority _info)
    (and priority (format "<span class=\"priority\">[%c]</span>" priority)))

  (defun org-html--tags (tags info)
    (when tags
      (format "<span class=\"tag\">%s</span>"
  	    (mapconcat
  	     (lambda (tag)
  	       (format "<span class=\"%s\">%s</span>"
  		       (concat (plist-get info :html-tag-class-prefix)
  			       (org-html-fix-class-name tag))
  		       tag))
  	     tags "&#xa0;"))))
#+end_src

è¿™ä¸‰ç©æ„æ²¡ä»€ä¹ˆå¥½è¯´çš„ï¼Œæˆ‘æ‰€æœ‰çš„åšå®¢éƒ½å‡ ä¹æ²¡æœ‰ä½¿ç”¨è¿‡ã€‚ä¸‹é¢æ˜¯æ”¹è¿›åçš„ç‰ˆæœ¬ï¼š

#+begin_src elisp
  (defun t--todo (todo info)
    "Format TODO keywords into HTML."
    (declare (ftype (function ((or null string) list) (or null string)))
             (important-return-value t))
    (when todo
      (let* ((prefix (t--pget info :html-todo-kwd-class-prefix))
             (common (t--pget info :html-todo-class))
             (status (if (member todo (cons "DONE" org-done-keywords))
                         "done" "todo")))
        (format "<span class=\"%s%s\">%s</span>"
                (concat prefix status)
                (if-let* ((c (t--nw-trim common))) (concat " " c) "")
                todo))))

  (defun t--priority (priority info)
    "Format a priority into HTML."
    (declare (ftype (function ((or null fixnum) list) (or null string)))
             (important-return-value t))
    (when priority
      (let ((class (t--pget info :html-priority-class)))
        ;; %c means produce a number as a single character.
        (format "<span%s>[%c]</span>"
                (if-let* ((c (t--nw-trim class)))
                    (format " class=\"%s\"" c) "")
                priority))))

  (defun t--tags (tags info)
    "Format TAGS into HTML."
    (declare (ftype (function (list list) (or null string)))
             (important-return-value t))
    (when-let* ((f (lambda (tag) (format "<span>%s</span>" tag)))
                (spans (t--nw-p (mapconcat f tags "&#xa0;"))))
      (if-let* ((class (t--nw-trim (t--pget info :html-tag-class))))
          (format "<span class=\"%s\">%s</span>"
                  class spans)
        (format "<span>%s</span>" spans))))
#+end_src

éœ€è¦æ³¨æ„çš„æ˜¯ï¼Œâ€‹=:with-todo-keywords= (=org-export-with-todo-keywords=) é»˜è®¤å€¼ä¸º =t=, =:with-priority= (=org-export-with-priority=) é»˜è®¤å€¼ä¸º =nil=, =:with-tags= (=org-export-with-tags=) é»˜è®¤å€¼ä¸º =t=â€‹ï¼Œè¿™ä¹Ÿå°±æ˜¯è¯´é™¤ä¼˜å…ˆçº§å¤–å…¶ä»–ä¸¤è€…é»˜è®¤å¯¼å‡ºã€‚

ä»¥ä¸‹ä»£ç è¢«ç”¨äºç”Ÿæˆ headline çš„æ ‡é¢˜éƒ¨åˆ†ï¼š

#+begin_src elisp
  (defun t-format-headline-default-function (todo priority text tags info)
    "Default format function for a headline.
  See `org-w3ctr-format-headline-function' for details and the
  description of TODO, PRIORITY, TEXT, TAGS, and INFO arguments."
    (declare (ftype (function ((or null string) (or null string)
                               (or null string) list list)
                              string)))
    (let ((todo (t--todo todo info))
          (priority (t--priority priority info))
          (tags (t--tags tags info)))
      (concat todo (and todo " ")
              priority (and priority " ")
              text (and tags "&#xa0;&#xa0;&#xa0;") tags)))

  (defun t--build-base-headline (headline info)
    "Build the inner HTML content of a headline.

  This function extracts all components of a HEADLINE element (like
  TODO keyword, priority, title, and tags) from the parse tree. It
  respects export options like `:with-todo-keywords' and `:with-tags'.

  Then, it passes these extracted components as arguments to the
  user-defined formatting function (from `:html-format-headline-function')
  to construct the final string."
    (declare (ftype (function (t list) string))
             (important-return-value t))
    (let* ((fn (lambda (prop) (org-element-property prop headline)))
           (todo (and-let* (((t--pget info :with-todo-keywords))
                            (todo (funcall fn :todo-keyword)))
                   (org-export-data todo info)))
           (priority (and (t--pget info :with-priority)
                          (funcall fn :priority)))
           (text (org-export-data (funcall fn :title) info))
           (tags (and (t--pget info :with-tags)
                      (org-export-get-tags headline info)))
           (f (t--pget info :html-format-headline-function)))
      (funcall f todo priority text tags info)))
#+end_src

ä¹Ÿè®¸ä½ å¯ä»¥æ³¨æ„åˆ° =org-w3ctr-format-headline-default-function= çš„å‚æ•°åˆ—è¡¨ä¸­å¹¶æ²¡æœ‰ =todo-type= å‚æ•°ï¼Œè¿™æ˜¯å› ä¸º ox-html å¹¶æ²¡æœ‰ä½¿ç”¨è¿™ä¸ªå‚æ•°ï¼Œæˆ‘å°±ç›´æ¥å»æ‰äº†ã€‚

** æ ‡é¢˜å±‚çº§

=org-html-headline= ä½¿ç”¨äº†å¦‚ä¸‹ä»£ç æ¥è·å–æ ‡é¢˜çš„å±‚çº§ï¼š

#+begin_src elisp
  (let ((...
         (level (+ (org-export-get-relative-level headline info)
                   (1- (plist-get info :html-toplevel-hlevel))))
         ...))
    ...)
#+end_src

å…¶ä¸­ï¼Œâ€‹=org-export-get-relative-level= ç”¨äºè·å–æ ‡é¢˜çš„ç›¸å¯¹å±‚çº§ï¼Œæ­¤å¤„çš„â€œç›¸å¯¹â€æŒ‡çš„æ˜¯æ ‡é¢˜å‚æ•°åœ¨æ•´ä¸ªæ–‡æ¡£ä¸­çš„ç›¸å¯¹å±‚çº§ã€‚â€‹=org-export--get-min-level= ä¼šè·å–æ•´ä¸ªæ–‡æ¡£çš„â€œæœ€å°å±‚çº§â€ï¼Œä»¥æ­¤ä½œä¸ºå…¶ä»–æ ‡é¢˜çš„ç›¸å¯¹å±‚çº§ï¼š

#+begin_src elisp
  (let* ((val nil)
         (f (lambda (res) (prog1 res (push res val)))))
    (advice-add 'org-export--get-min-level :filter-return f)
    (unwind-protect
        (progn
          (org-export-string-as "* a" 'html t)
          (org-export-string-as "** a" 'html t)
          (org-export-string-as "*** a" 'html t)
          (reverse val))
      (advice-remove 'org-export--get-min-level f)))
  ;;=> (1 2 3)
#+end_src

å¦‚æœæŸä¸ªæ–‡æ¡£çš„æœ€å°ï¼ˆæ³¨æ„æ˜¯ =min= ä¸æ˜¯ =low=â€‹ï¼‰å±‚çº§ä¸º 1ï¼Œé‚£ä¹ˆ =*= æ ‡é¢˜çš„ç›¸å¯¹å±‚çº§ä¸º 1ï¼Œâ€‹=**= ä¸º 2ï¼›å¦‚æœæŸä¸ªæ–‡æ¡£çš„æœ€å°å±‚çº§ä¸º 2ï¼ˆäºŒçº§æ ‡é¢˜ï¼‰ï¼Œé‚£ä¹ˆ =**= çš„ç›¸å¯¹å±‚çº§ä¸º 1ï¼Œâ€‹â€‹=***= ä¸º 2......ä»¥æ­¤ç±»æ¨ã€‚

=:html-toplevel-hlevel= æ˜¯ ox-html æ·»åŠ çš„é€‰é¡¹ï¼Œè¡¨ç¤ºç›¸å¯¹å±‚çº§ä¸º 1 çš„æ ‡é¢˜å¯¹åº”åˆ° HTML çš„æ ‡é¢˜çº§åˆ«ï¼Œé»˜è®¤å€¼ =org-html-toplevel-hlevel= ä¸º 2ï¼Œå¯¹åº”äº HTML çš„ H2 æ ‡é¢˜ã€‚è¿™ä¹Ÿå°±æ˜¯ä¸ºä»€ä¹ˆæœ¬å°èŠ‚å¼€å¤´çš„è¡¨è¾¾å¼è¿˜ä¼šå‡ä¸€ï¼Œæ¯•ç«Ÿ =1 + 2 - 1 = 2=â€‹ã€‚é‚£ä¹ˆï¼Œä¸ºä»€ä¹ˆ =:html-toplevel-hlevel= çš„é»˜è®¤å€¼ä¸º 2 è€Œä¸æ˜¯ 1 å‘¢ï¼Ÿè¿™æ¶‰åŠåˆ° HTML æ˜¯å¦èƒ½å¤Ÿå®¹çº³å¤šä¸ª H1 æ ‡ç­¾çš„é—®é¢˜ï¼Œ[[https://blog.shimin.io/should-you-use-multiple-h1-s-in-2022/][Should you use multiple <h1> heading elements on your page in 2022?]] è¿™ç¯‡åšå®¢ç»™å‡ºäº†ä¸é”™çš„è¯´æ˜ï¼Œä¸‹é¢ç®€å•åšä¸ªæ€»ç»“ã€‚

#+begin_quote
The web is full of comments that attack some deeply held web development
beliefs. Something like 'you should use multiple H1s on a page, it's no
longer 2001'. Maybe you are a bit old-fashioned and, like me, still
clutching the principle of /Only One H1 Per Page/ pretty tightly in your
internal style dictionary. So you decided to look it up.

But it turns out, this question goes pretty deep.

TL;DR: multiple =<h1>=â€‹s are technically on specification, but the document
outline algorithm is dead so either create one =<h1>= or split the page
into 2 or more pages.
#+end_quote

*** HTML çš„æ ‡é¢˜å…ƒç´  =<h1>-<h6>=

ä»¥ä¸‹å†…å®¹æ€»ç»“è‡ª MDN çš„ [[https://developer.mozilla.org/en-US/docs/Web/HTML/Reference/Elements/Heading_Elements][<h1>â€“<h6>: The HTML Section Heading elements]]ã€‚

åœ¨ HTML ä¸­ï¼Œâ€‹=<h1>= åˆ° =<h6>= å…ƒç´ è¢«ç”¨æ¥å‘ˆç°å…­ä¸ªä¸åŒçš„çº§åˆ«çš„æ ‡é¢˜ï¼Œâ€‹=<h1>= çº§åˆ«æœ€é«˜ï¼Œâ€‹=<h6>= çº§åˆ«æœ€ä½ã€‚é»˜è®¤æƒ…å†µä¸‹ï¼Œæ ‡é¢˜å…ƒç´ ä½¿ç”¨å—çº§å¸ƒå±€ã€‚

åœ¨ä½¿ç”¨å»ºè®®ä¸Šï¼ŒMDN æåˆ°ï¼š

- æ ‡é¢˜ä¿¡æ¯å¯è¢«ç”¨æˆ·ä»£ç†ï¼ˆUser agentï¼‰ç”¨æ¥è‡ªåŠ¨æ„å»ºæ–‡æ¡£ç›®å½•
- ä¸è¦ç”¨æ ‡é¢˜å…ƒç´ æ¥è°ƒèŠ‚æ–‡æœ¬å¤§å°ï¼Œåº”ä½¿ç”¨ CSS çš„ =font-size= å±æ€§
- ä¸è¦è·³è¿‡æ ‡é¢˜å±‚çº§ï¼šæ€»æ˜¯ä» =<h1>= å¼€å§‹ï¼Œç´§éš =<h2>= å¹¶ä»¥æ­¤ç±»æ¨

å¯¹äºæ˜¯å¦èƒ½å¤Ÿåœ¨æ–‡æ¡£ä¸­å¼•å…¥å¤šä¸ª =<h1>= æ ‡ç­¾ï¼ŒMDN è¡¨ç¤ºä¸å»ºè®®ï¼š

#+begin_quote
While using multiple =<h1>= elements on one page is allowed by the HTML
standard (as long as they are not nested), this is not considered a best
practice. A page should generally have a single =<h1>= element that
describes the content of the page (similar to the document's =<title>=
element).

#+attr__: [note]
Nesting multiple =<h1>= elements in nested [[https://developer.mozilla.org/en-US/docs/Web/HTML/Reference/Elements#content_sectioning][sectioning elements]] was
allowed in older versions of the HTML standard. However, this was never
considered a best practice and is now non-conforming. Read more in [[https://adrianroselli.com/2016/08/there-is-no-document-outline-algorithm.html][There
Is No Document Outline Algorithm]].

Prefer using only one <h1> per page and nest headings without skipping
levels.
#+end_quote

è¿™ä¸€é¡µé¢çš„å¦ä¸€ç›¸å…³ä¿¡æ¯æ˜¯ HTML æ ‡å‡†æ›¾ç»ï¼ˆ2025 å¹´ 5 æœˆä¹‹å‰ï¼‰æŒ‡å®š =<section>=, =<article>=, =<aside>= å’Œ =<nav>= ä¸­çš„ =<h1>= å…ƒç´ åº”è¯¥å½“ä½œ =<h2>= æˆ– =<h3>= è¿›è¡Œæ¸²æŸ“ï¼Œä½†è¿™ä¸€ä¸Šä¸‹æ–‡ç‰¹å®šè¡Œä¸ºç°åœ¨å·²ç»ä»æ ‡å‡†ä¸­[[https://github.com/whatwg/html/issues/7867][ç§»é™¤]]äº†ã€‚

åŸºäºè¿™ä¸€è¡Œä¸ºï¼Œæˆ‘ä½¿ç”¨äº†å¦‚ä¸‹å®ç°æ¥è·å– Org-mode æ ‡é¢˜å¯¹åº”çš„ HTML æ ‡é¢˜å…ƒç´ çº§åˆ«ï¼š

#+begin_src elisp
  (defun t--get-headline-hlevel (headline info)
    "Calculate the absolute HTML heading level for a headline.

  This function computes the final HTML heading level based on the
  headline's relative level within the Org document and the value
  of `:html-toplevel-hlevel'. The formula used is:
    (relative-level + top-level - 1).

  It also validates that `:html-toplevel-hlevel' is an integer
  between 2 and 6, signaling an error if it is not."
    (declare (ftype (function (t list) fixnum))
             (important-return-value t))
    (let ((top-level (t--pget info :html-toplevel-hlevel))
          (level (org-export-get-relative-level headline info)))
      (unless (and (fixnump top-level) (<= 2 top-level 6))
        (t-error "Invalid HTML top level: %s" top-level))
      (+ level top-level -1)))

  (defun t--headline-hN (headline info)
    "Return the HTML heading tag name (e.g., \"h2\") for HEADLINE.

  The level is capped at 6, so this function always returns a
  string from \"h1\" to \"h6\"."
    (declare (ftype (function (t list) string))
             (important-return-value t))
    (let* ((level (min 6 (t--get-headline-hlevel headline info))))
      (format "h%s" level)))
#+end_src

ä»£ç ä¸­çš„ ~(<= 2 top-level 6)~ ä¹Ÿå¯ä»¥æ”¹ä¸º 1 åˆ° 6ï¼Œä¸è¿‡ä¸€èˆ¬æ¥è¯´è¿™ç”¨ä¸ä¸Šã€‚

** ä½å±‚çº§æ ‡é¢˜

æ—¢ç„¶ HTML åªèƒ½æ”¯æŒæœ€å¤š 5 çº§åˆ«æ ‡é¢˜ï¼ˆä¸€èˆ¬åªåœ¨æ–‡æ¡£å¼€å¤´ç”¨ä¸€ä¸ªå’Œ =<title>= ä¸€è‡´çš„ =<h1>= ä½œä¸ºæ–‡æ¡£æ ‡é¢˜ï¼‰ï¼Œé‚£ä¹ˆ Org-mode æ–‡æ¡£ä¸­è¶…è¿‡ 5 çº§ (=*****=) çš„ headline è¦å¦‚ä½•å¤„ç†å‘¢ï¼Ÿox ä¸­æœ‰ä¸€ä¸ª =:headline-levels= é€‰é¡¹æ¥å†³å®šå¯¼å‡ºæ ‡é¢˜çš„æœ€å¤§ï¼ˆæ³¨æ„ä¸æ˜¯æœ€é«˜ï¼‰headline å±‚çº§ï¼š

#+begin_src elisp
  (defcustom org-export-headline-levels 3
    "The last level which is still exported as a headline.

  Inferior levels will usually produce itemize or enumerate lists
  when exported, but backend behavior may differ.

  This option can also be set with the OPTIONS keyword,
  e.g. \"H:2\"."
    :group 'org-export-general
    :type 'integer
    :safe #'integerp)

  (defun org-export-low-level-p (headline info)
    "Non-nil when HEADLINE is considered as low level.

  INFO is a plist used as a communication channel.

  A low level headlines has a relative level greater than
  `:headline-levels' property value.

  Return value is the difference between HEADLINE relative level
  and the last level being considered as high enough, or nil."
    (let ((limit (plist-get info :headline-levels)))
      (when (wholenump limit)
        (let ((level (org-export-get-relative-level headline info)))
          (and (> level limit) (- level limit))))))
#+end_src

åœ¨é»˜è®¤æƒ…å†µä¸‹ï¼Œä¸€æ—¦ headline çš„å±‚çº§è¶…è¿‡ä¸‰çº§ï¼Œä¸€ä¸ªå°Šé‡è¯¥é€‰é¡¹çš„ Org-mode å¯¼å‡ºåç«¯ï¼ˆæ¯”å¦‚ ox-htmlï¼‰ä¸ä¼šå°†è¯¥ headline å¯¼å‡ºåˆ°ç›®æ ‡æ ¼å¼çš„ç« èŠ‚æ ‡é¢˜ï¼Œè€Œå¯èƒ½æ˜¯åˆ—è¡¨æˆ–å…¶ä»–å…ƒç´ ï¼Œåœ¨ ox-html ä¸­ =***= ä¼šè¢«å¯¼å‡ºåˆ° =<h4>=, =****= æ ‡é¢˜ä¼šè¢«å¯¼å‡ºåˆ° =<ul>= æˆ– =<ol>=â€‹ã€‚

#+begin_src text
  org-export-headline-levels is a variable defined in â€˜ox.elâ€™.

  Its value is 3

  The last level which is still exported as a headline.

  Inferior levels will usually produce itemize or enumerate lists
  when exported, but backend behavior may differ.
#+end_src

å½“ç„¶ï¼Œåœ¨æˆ‘çœ‹æ¥è¿™æ˜¯æœ‰ä¸€ç‚¹â€œæµªè´¹â€çš„ï¼Œå› æ­¤æˆ‘ä½¿ç”¨å¦‚ä¸‹å‡½æ•°æ¥åˆ¤æ–­æŸä¸ª headline æ˜¯å¦æ˜¯ä½å±‚çº§æ ‡é¢˜ï¼š

#+begin_src elisp
  (defcustom t-honor-ox-headline-levels nil
    "Honor `org-export-headline-levels' or not."
    :group 'org-export-w3ctr
    :type 'boolean)

  (defun t--low-level-headline-p (headline info)
    "Check if HEADLINE should be rendered as a low-level list item.

  This predicate determines if a headline's level exceeds the
  standard HTML heading range (i.e., <h6>).

  Its behavior depends on `:html-honor-ox-headline-levels':
  - If non-nil, it uses the default `org-export-low-level-p'.
  - If nil, it uses a custom check based on the calculated h-level
    from `org-w3ctr--get-headline-hlevel'."
    (declare (ftype (function (t list) boolean))
             (important-return-value t))
    (if-let* ((honor (t--pget info :html-honor-ox-headline-levels)))
        (org-export-low-level-p headline info)
      (let ((level (t--get-headline-hlevel headline info)))
        (> level 6))))
#+end_src

å¦‚æœç”¨æˆ·é€‰æ‹©ä¸å°Šé‡ =:headline-levels= é€‰é¡¹ï¼Œé‚£ä¹ˆä»…å½“ =org-w3ctr--get-headline-hlevel= è¿”å›å€¼å¤§äº 6 æ—¶æ‰ä¼šè¢«å½“ä½œä½å±‚çº§æ ‡é¢˜ã€‚åœ¨ ox-w3ctr ä¸­æˆ‘é€‰æ‹©é»˜è®¤ä¸å°Šé‡ oxï¼ˆç¬‘ï¼‰ã€‚

åœ¨ =org-html-headline= ä¸­ï¼Œä½å±‚çº§æ ‡é¢˜ä½¿ç”¨äº†å¦‚ä¸‹ä»£ç è¿›è¡Œå¯¼å‡ºï¼š

#+begin_src elisp
  (let* ((html-type (if numberedp "ol" "ul")))
    (concat
     (and (org-export-first-sibling-p headline info)
  	(apply #'format "<%s class=\"org-%s\">\n"
  	       (make-list 2 html-type)))
     (org-html-format-list-item
      contents (if numberedp 'ordered 'unordered)
      nil info nil
      (concat (org-html--anchor id nil nil info) formatted-text)) "\n"
     (and (org-export-last-sibling-p headline info)
  	(format "</%s>\n" html-type))))
#+end_src

ä½ å¯ä»¥æ³¨æ„åˆ°å®ƒä½¿ç”¨äº† =org-html-format-list-item= è¿™ä¸€å‡½æ•°æ¥ç”Ÿæˆåˆ—è¡¨é¡¹ï¼Œåœ¨æˆ‘çœ‹æ¥è¿™å¸¦æ¥äº†ä¸å¿…è¦çš„è€¦åˆï¼Œæˆ‘æŠŠ headline åˆ—è¡¨é¡¹çš„ç”Ÿæˆé€»è¾‘ä» =org-html-format-list-item= æŠ½å‡ºæ¥æ”¾åˆ°äº† =org-w3ctr--build-low-level-headline= ä¸­ï¼š

#+begin_src elisp
  (defun t--build-low-level-headline (headline contents info)
    "Transcode a low-level headline into an HTML list item (`<li>').

  This function renders headlines that are too deep to become standard
  <hN> tags. It creates a list structure where a group of sibling
  low-level headlines becomes a single `<ol>' or `<ul>'.

  The list type (`<ol>' vs. `<ul>') is determined by whether section
  numbering is active."
    (declare (ftype (function (t t list) string))
             (important-return-value t))
    (let* ((numberedp (org-export-numbered-headline-p headline info))
           (tag (if numberedp "ol" "ul"))
           (text (t--build-base-headline headline info))
           (id (t--reference headline info)))
      (concat
       (and (org-export-first-sibling-p headline info)
            (format "<%s>\n" tag))
       "<li>" (format "<span id=\"%s\"></span>" id) text
       (when-let* ((c (t--nw-p contents))) (concat "<br>\n" c))
       "</li>\n"
       (and (org-export-last-sibling-p headline info)
            (format "</%s>\n" tag)))))
#+end_src

** å†…å®¹åŒºå—

åœ¨ Org-mode ä¸­ï¼Œä½çº§ headline å±äºå®ƒçš„çˆ¶ headlineï¼Œä¹Ÿå°±æ˜¯è¯´ headline ä¹‹é—´æ˜¯å­˜åœ¨åµŒå¥—å…³ç³»çš„ã€‚é‚£ä¹ˆï¼Œåœ¨ç”Ÿæˆ HTML æ—¶åº”è¯¥é‡‡ç”¨ä»¥å“ªç§æ–¹å¼å‘¢ï¼Ÿ

#+begin_src html
  <!-- ONE -->
  <section>
    <h1>T1</h1> <p>Hello</p>
    <h2>T2</h2> <p>World</p>
    <h3>T3</h3> <p>!</p>
  </section>

  <!-- TWO -->
  <section>
    <h1>T1</h1> <p>Hello</p>
    <section>
      <h2>T2</h2> <p>World</p>
      <section>
        <h3>T3</h3> <p>!</p>
      </section>
    </section>
  </section>
#+end_src

è¿™ä¸¤ç§æ–¹å¼æˆ‘åœ¨ W3C æ ‡å‡†ä¸­éƒ½çœ‹åˆ°è¿‡ï¼Œå‰è€…æœ‰ [[https://www.w3.org/TR/2024/WD-css-values-4-20240312/][CSS Values and Units Module Level 4]]ï¼Œåè€…æœ‰ [[https://www.w3.org/TR/2025/REC-png-3-20250624/][Portable Network Graphics (PNG) Specification (Third Edition)]]ã€‚æˆ‘é—®äº†ä¸€ä¸‹ geminiï¼Œä»¥ä¸‹æ˜¯å®ƒçš„è¯„ä»·ï¼š

- W3C CSS Values and Units Module Level 3 ::
  æ ‡é¢˜å…ƒç´ ç›´æ¥ä½äºæ–‡æœ¬æµä¸­ï¼Œæ²¡æœ‰ä½¿ç”¨ =<section>= æˆ–å…¶ä»–è¯­ä¹‰å®¹å™¨æ¥æ˜ç¡®åŒ…è£¹å…¶å†…å®¹ï¼Œæ˜¯æ‰å¹³åŒ–çš„æ ‡é¢˜ç»“æ„ã€‚
- W3C PNG Specification ::
  ä½¿ç”¨ =<section>= å…ƒç´ æ˜ç¡®åŒ…è£¹æ¯ä¸ªç« èŠ‚åŠå…¶å†…å®¹ï¼Œç¬¦åˆ HTML5 æœ€ä½³å®è·µï¼Œä½†æ˜¯ HTML æ ‡è®°ä¼šç¨å¤šä¸€äº›ã€‚

å¦‚æœæˆ‘ä»¬ä½¿ç”¨ ox-html æ¥å¯¼å‡ºæœ¬æ–‡æ¡£ï¼Œå¾—åˆ°çš„éƒ¨åˆ† HTML å¦‚ä¸‹ï¼š

#+begin_src html
  <div id="outline-container-org54bf526" class="outline-3">
    <h3 id="org54bf526"><span class="section-number-3">2.3.</span> ä½å±‚çº§æ ‡é¢˜</h3>
    <div class="outline-text-3" id="text-2-3">
      ...
    </div>
  </div>
  <div id="outline-container-org373137f" class="outline-3">
    <h3 id="org373137f"><span class="section-number-3">2.4.</span> å†…å®¹åŒºå—</h3>
    <div class="outline-text-3" id="text-2-4">
      ...
    </div>
  </div>
#+end_src

ä»å†å²æ²¿é©å’Œè·Ÿéšæœ€æ–°æ ‡å‡†çš„è§’åº¦æ¥çœ‹ï¼Œæˆ‘è¿˜æ˜¯é€‰æ‹©äº†åµŒå¥— =<section>= çš„åšæ³•ï¼š

#+begin_src elisp
  (defcustom t-container-element "section"
    "The HTML tag name for the element that contains a headline.

    Common values are \"section\" or \"div\". If nil, \"div\" is used."
    :group 'org-export-w3ctr
    :type '(choice string (const nil)))

  ;; FIXME: Add container checker here.
  (defun t--headline-container (headline info)
    "Return HTML container name for HEADLINE as a string."
    (declare (ftype (function (t list) string))
             (important-return-value t))
    (or (org-element-property :HTML_CONTAINER headline)
        (t--pget info :html-container)
        "div"))
#+end_src

** è‡ªé“¾æ¥

å½“ =:html-self-link-headlines= (=org-html-self-link-headlines=) ä¸ºéç©ºå€¼æ—¶ï¼Œox-html åœ¨å¯¼å‡º headline æ—¶ä¼šä½¿ç”¨ =<a>= æ ‡ç­¾åŒ…è£¹æ ‡é¢˜è®©å®ƒå¯ç‚¹å‡»ï¼š

#+begin_src html
  <div id="outline-container-org0040c93" class="outline-3">
    <h3 id="org0040c93">
      <span class="section-number-3">2.5.</span>
      <a href="#org0040c93">è‡ªé“¾æ¥ä¸æ ‡é¢˜åºå·</a></h3>
    <div class="outline-text-3" id="text-2-5">
      ...
    </div>
  </div>
#+end_src

åœ¨ W3C æ ‡å‡†æ–‡æ¡£ä¸­ï¼Œself-link æ˜¯ä½äºæ ‡é¢˜åçš„ =<a>= ç©ºå†…å®¹æ ‡ç­¾ï¼Œä½¿ç”¨ CSS æ¥æåˆ°æ ‡é¢˜ä¹‹å‰ï¼š

#+begin_src mhtml
  <style>
    a.self-link {
        position: absolute;
        top: 0;
        left: calc(-1 * (3.5rem - 26px));
        /* width: calc(3.5rem - 26px); */
        height: 2em;
        text-align: center;
        border: none;
        transition: opacity .2s;
        opacity: .5;
    }
    :is(h2,h3,h4,h5,h6)+a.self-link::before{
        content:"Â§";
        text-decoration:none;
        color:var(--heading-text)
    }
  </style>

  <section id="orgnh-2.5">
    <div class="header-wrapper">
      <h3 id="x-orgnh-2.5"><span class="secno">2.5. </span>è‡ªé“¾æ¥ä¸æ ‡é¢˜åºå·</h3>
      <a class="self-link" href="#orgnh-2.5" aria-label="Link to this section"></a>
    </div>
    ...
  </section>
#+end_src

åœ¨ ox-w3ctr ä¸­ï¼Œæˆ‘ä¿ç•™äº† =:html-self-link-headlines= é€‰é¡¹å¹¶é»˜è®¤â€‹*å¼€å¯*â€‹ï¼š

#+begin_src elisp
  (defcustom t-self-link-headlines t
    "When non-nil, the headlines contain a hyperlink to themselves."
    :group 'org-export-w3ctr
    :type 'boolean
    :safe #'booleanp)
#+end_src

** æ ‡é¢˜åºå·

åœ¨ =org-html-headline= ä¸­ï¼Œæ ‡é¢˜åºå·çš„ç›¸å…³ä»£ç å¦‚ä¸‹ï¼š

#+begin_src elisp
  (let* ((numberedp (org-export-numbered-headline-p headline info))
         (numbers (org-export-get-headline-number headline info))
         ...)
    ...
    (and numberedp
         (format
          "<span class=\"section-number-%d\">%s</span> "
          level
          (concat (mapconcat #'number-to-string numbers ".") ".")))
    ...)
#+end_src

æˆ‘ä½¿ç”¨äº†å¦‚ä¸‹ä»£ç æ¥ç”Ÿæˆæ ‡é¢˜åºå·ï¼š

#+begin_src elisp
  (defun t--headline-secno (headline info)
    "Return section number for HEADLINE as an HTML span."
    (declare (ftype (function (t list) (or null string)))
             (important-return-value t))
    (when-let* ((numbers (and (org-export-numbered-headline-p headline info)
                              (org-export-get-headline-number headline info))))
      (format "<span class=\"secno\">%s. </span>"
              (mapconcat #'number-to-string numbers "."))))
#+end_src

** æ™®é€š headline

åœ¨å®ŒæˆåŠŸèƒ½æ‹†åˆ†åï¼Œheadline çš„â€œä¸»å‡½æ•°â€å°±ç›¸å¯¹ç®€å•äº†ï¼š

#+begin_src elisp
  (defun t--build-normal-headline (headline contents info)
    "Build HTML for a standard headline and its section.

  This function formats a regular headline, which is not a footnote
  or a low-level headline treated as a list item."
    (let* ((secno (t--headline-secno headline info))
           (h (t--headline-hN headline info))
           (text (t--build-base-headline headline info))
           (full-text (concat secno text))
           (id (t--reference headline info))
           (c (t--headline-container headline info))
           (c-cls (org-element-property :HTML_CONTAINER_CLASS headline))
           (h-cls (org-element-property :HTML_HEADLINE_CLASS headline)))
      ;; <C>, id, class, header, contents, </C>
      (format "<%s id=\"%s\"%s>\n%s%s</%s>\n"
              c id (or (and c-cls (format " class=\"%s\"" c-cls)) "")
              (format
               ;; <H>, id, class, headline, </H>
               ;; FIXME: is x-id necessary?
               (concat "<div class=\"header-wrapper\">\n"
                       "<%s id=\"x-%s\"%s>%s</%s>\n"
                       (t--headline-self-link id info)
                       "</div>\n")
               h id (or (and h-cls (format " class=\"%s\"" h-cls)) "")
               full-text h)
              (or contents "") c)))

  (defun t-headline (headline contents info)
    "Transcode a HEADLINE element from Org to HTML.
  CONTENTS holds the contents of the headline.  INFO is a plist
  holding contextual information."
    (unless (org-element-property :footnote-section-p headline)
      (if (t--low-level-headline-p headline info)
          ;; This is a deep sub-tree: export it as a list item.
          (t--build-low-level-headline headline contents info)
        ;; Normal headline.  Export it as a section.
        (t--build-normal-headline headline contents info))))
#+end_src

* ç›®å‰å­˜åœ¨çš„é—®é¢˜

åœ¨å®Œæˆé‡æ„åï¼Œå’Œ headline å¯¼å‡ºç›¸å…³çš„ä»£ç å¤§æ¦‚æœ‰ 250 è¡Œå·¦å³ï¼Œå’Œ headline ç›¸å…³çš„é€‰é¡¹å¤§æ¦‚æœ‰ 10 ä¸ªï¼Œ50 è¡Œã€‚æˆ‘é—®äº†ä¸‹ geminiï¼Œè¿™æ˜¯å®ƒçš„è¯„ä»·ã€‚

** gemini çš„è¯„ä»·

è¿™æ®µä»£ç æ˜¯ Emacs Lispï¼Œç”¨äºä¸º Org Mode åˆ›å»ºä¸€ä¸ªè‡ªå®šä¹‰çš„ HTML å¯¼å‡ºåç«¯ã€‚å…¶ç›®çš„æ˜¯å¯¹ Org æ–‡ä»¶ä¸­çš„æ ‡é¢˜ï¼ˆHeadlineï¼‰å¦‚ä½•è½¬æ¢ä¸º HTML æä¾›ç²¾ç»†çš„æ§åˆ¶ã€‚

æ€»ä½“è€Œè¨€ï¼Œè¿™æ˜¯ä¸€æ®µé«˜è´¨é‡ã€ç²¾å¿ƒç¼–å†™çš„ Elisp ä»£ç ï¼Œå±•ç°äº†ä½œè€…å¯¹ Emacs Lisp å’Œ Org Mode å¯¼å‡ºå™¨å†…éƒ¨æœºåˆ¶çš„æ·±åˆ»ç†è§£ã€‚ä»£ç ç»“æ„æ¸…æ™°ã€æ–‡æ¡£é½å…¨ã€å¹¶éµå¾ªäº†è‰¯å¥½çš„ç¼–ç¨‹å®è·µã€‚

*** æ€»ä½“è¯„ä»·

è¿™æ®µä»£ç å®ç°äº†ä¸€ä¸ªåŠŸèƒ½å¼ºå¤§ä¸”é«˜åº¦å¯å®šåˆ¶çš„ Org Mode æ ‡é¢˜ HTML å¯¼å‡ºå™¨ã€‚å®ƒé€šè¿‡å®šä¹‰ä¸€ç³»åˆ—ç”¨æˆ·å¯é…ç½®çš„å˜é‡ (=defcustom=) å’Œä¸€ç»„ååŒå·¥ä½œçš„å‡½æ•°ï¼ŒæˆåŠŸåœ°å°†æ ‡é¢˜çš„å„ä¸ªç»„æˆéƒ¨åˆ†ï¼ˆTODO çŠ¶æ€ã€ä¼˜å…ˆçº§ã€æ ‡ç­¾ã€æ ‡é¢˜æ–‡æœ¬ç­‰ï¼‰æŠ½è±¡å‡ºæ¥ï¼Œå¹¶å…è®¸ç”¨æˆ·é€šè¿‡è‡ªå®šä¹‰å‡½æ•°è‡ªç”±ç»„åˆï¼Œæœ€ç»ˆç”Ÿæˆç°ä»£ã€è¯­ä¹‰åŒ–çš„ HTML ç»“æ„ã€‚

*** ä¼˜ç‚¹ (Strengths) ğŸ‘

- æ¨¡å—åŒ–ä¸å¯è¯»æ€§é«˜ï¼šä»£ç è¢«æ‹†åˆ†æˆè®¸å¤šå°çš„ã€åŠŸèƒ½å•ä¸€çš„å‡½æ•°ï¼ˆå¦‚ =t--todo=, =t--priority=, =t--tags=â€‹ï¼‰ï¼Œæ¯ä¸ªå‡½æ•°åªè´Ÿè´£ä¸€é¡¹ä»»åŠ¡ã€‚â€‹=t-= å’Œ =t--= çš„å‰ç¼€æ¸…æ™°åœ°åŒºåˆ†äº†å…¬å…± API å’Œå†…éƒ¨è¾…åŠ©å‡½æ•°ï¼Œè¿™æ˜¯ Elisp ç¼–ç¨‹çš„ä¼˜ç§€å®è·µã€‚

- é«˜åº¦å¯å®šåˆ¶ï¼šé€šè¿‡ =defcustom= æä¾›äº†ä¸°å¯Œçš„è‡ªå®šä¹‰é€‰é¡¹ï¼Œç”¨æˆ·å¯ä»¥é€šè¿‡ Emacs çš„ M-x customize ç•Œé¢è½»æ¾é…ç½® CSS ç±»åã€æ ‡é¢˜çº§åˆ«ã€å®¹å™¨å…ƒç´ ç­‰ï¼Œè€Œæ— éœ€ä¿®æ”¹ä»£ç ã€‚å°†æ ¸å¿ƒæ ¼å¼åŒ–é€»è¾‘å§”æ‰˜ç»™ä¸€ä¸ªå¯é…ç½®çš„å‡½æ•° (=t-format-headline-function=) æ˜¯ä¸€ä¸ªéå¸¸çµæ´»çš„è®¾è®¡ã€‚

- æ–‡æ¡£å®Œå–„ï¼šå‡ ä¹æ‰€æœ‰çš„å˜é‡å’Œå‡½æ•°éƒ½æœ‰è¯¦ç»†çš„æ–‡æ¡£å­—ç¬¦ä¸²ï¼ˆdocstringï¼‰ï¼Œæ¸…æ™°åœ°è§£é‡Šäº†å…¶ç”¨é€”ã€å‚æ•°å’Œè¿”å›å€¼ã€‚è¿™æå¤§åœ°æå‡äº†ä»£ç çš„å¯ç»´æŠ¤æ€§å’Œæ˜“ç”¨æ€§ã€‚

- ä¸¥è°¨çš„ç±»å‹å£°æ˜ï¼šä»£ç ä¸­ä½¿ç”¨äº† =(declare (ftype ...))= æ¥å£°æ˜å‡½æ•°ç±»å‹ï¼Œè¿™ä¸ä»…æœ‰åŠ©äºå…¶ä»–å¼€å‘è€…ç†è§£ä»£ç ï¼Œè¿˜èƒ½è®© Emacs çš„å­—èŠ‚ç¼–è¯‘å™¨è¿›è¡Œæ›´æœ‰æ•ˆçš„ä¼˜åŒ–å’Œç±»å‹æ£€æŸ¥ï¼Œæ˜¯ä¸“ä¸š Elisp å¼€å‘çš„æ ‡å¿—ã€‚

- å¥å£®æ€§ï¼šä»£ç è€ƒè™‘äº†å¤šç§ Org Mode çš„å¯¼å‡ºè®¾ç½®ï¼ˆå¦‚ =:with-tags=â€‹ã€â€‹=:section-numbers= ç­‰ï¼‰ï¼Œå¹¶æ­£ç¡®å¤„ç†äº†ä¸åŒå±‚çº§çš„æ ‡é¢˜ï¼ˆåŒ…æ‹¬å°†æ·±å±‚æ ‡é¢˜è½¬æ¢ä¸ºåˆ—è¡¨ï¼‰ï¼Œæ˜¾ç¤ºäº†å…¶è®¾è®¡çš„å‘¨å…¨ã€‚â€‹=t--get-headline-hlevel= ä¸­çš„é”™è¯¯æ£€æŸ¥ä¹Ÿå¢å¼ºäº†ä»£ç çš„é²æ£’æ€§ã€‚

*** æ”¹è¿›å»ºè®® (Suggestions) ğŸ’¡

å°½ç®¡ä»£ç å·²ç»éå¸¸å‡ºè‰²ï¼Œä½†ä»æœ‰ä¸€äº›å¯ä»¥æ¢è®¨å’Œå¾®è°ƒçš„åœ°æ–¹ï¼š

- å‘½åä¸€è‡´æ€§ï¼šâ€‹=defcustom= ä¸­å®šä¹‰çš„å˜é‡ä»¥ =t-= å¼€å¤´ï¼ˆå¦‚ =t-todo-class=â€‹ï¼‰ï¼Œä½†åœ¨å…¶æ–‡æ¡£å­—ç¬¦ä¸²ä¸­ï¼Œå¼•ç”¨çš„å´æ˜¯ =org-w3ctr-todo-class=â€‹ã€‚è™½ç„¶è¿™å¯èƒ½æ˜¯ä¸ºäº†ä¸æŸä¸ªåŒ…çš„å‘½åä¹ æƒ¯ä¿æŒä¸€è‡´ï¼Œä½†è¿™å¯èƒ½å¯¼è‡´ç”¨æˆ·æ··æ·†ã€‚å»ºè®®åœ¨æ–‡æ¡£å’Œä»£ç ä¸­ç»Ÿä¸€å‰ç¼€ï¼Œæˆ–è€…æ˜ç¡®è¯´æ˜ä¸¤è€…ä¹‹é—´çš„å…³ç³»ã€‚

- å¤„ç† FIXME æ³¨é‡Šï¼šä»£ç ä¸­æœ‰å‡ å¤„ FIXME æ³¨é‡Šï¼Œè¿™æ˜¯å¾ˆå¥½çš„è‡ªæˆ‘æé†’ï¼Œä½†ä¹ŸæŒ‡å‡ºäº†å¾…å®Œå–„ä¹‹å¤„ï¼š
  - =FIXME: consider malformed headline=â€‹ï¼šè¿™æ˜¯ä¸ªå¤æ‚é—®é¢˜ã€‚å¯ä»¥è€ƒè™‘å¢åŠ ä¸€äº›åŸºæœ¬çš„å®¹é”™å¤„ç†ï¼Œæˆ–è€…åœ¨æ–‡æ¡£ä¸­æ˜ç¡®æŒ‡å‡ºä»£ç æœŸæœ›æ¥æ”¶æ ¼å¼æ­£ç¡®çš„æ ‡é¢˜ã€‚
  - =FIXME: is x-id necessary?=â€‹ï¼šåœ¨ =t--build-normal-headline= ä¸­ï¼Œâ€‹=<h4>= ç­‰æ ‡é¢˜æ ‡ç­¾è¢«èµ‹äºˆäº† ~id="x-%s"~â€‹ï¼Œè€Œå…¶çˆ¶å®¹å™¨å·²ç»æœ‰äº† ~id="%s"~â€‹ã€‚è¿™å¯èƒ½å¯¼è‡´ HTML ä¸­å‡ºç°ä¸¤ä¸ªç›¸ä¼¼çš„ IDã€‚é™¤é =x-= å‰ç¼€çš„ ID æœ‰ç‰¹æ®Šçš„ JavaScript æˆ– CSS ç”¨é€”ï¼Œå¦åˆ™å¯ä»¥è€ƒè™‘ç§»é™¤å®ƒï¼Œä»¥ç”Ÿæˆæ›´ç®€æ´çš„ HTMLã€‚

- ç®€åŒ– =t--build-normal-headline= å‡½æ•°ï¼šè¿™ä¸ªå‡½æ•°ä¸­çš„ =format= è¯­å¥æ¯”è¾ƒé•¿ä¸”åµŒå¥—è¾ƒæ·±ï¼Œå¯è¯»æ€§ç¨å·®ã€‚å¯ä»¥è€ƒè™‘å°†å…¶æ‹†åˆ†ï¼Œæˆ–è€…ä½¿ç”¨ =concat= ç»“åˆè¾…åŠ©å˜é‡æ¥æ„å»ºæœ€ç»ˆçš„å­—ç¬¦ä¸²ï¼Œä½¿å…¶ç»“æ„æ›´æ¸…æ™°ã€‚ä¾‹å¦‚ï¼Œå¯ä»¥å…ˆæ„å»º =<hN>...</hN>= éƒ¨åˆ†ï¼Œå†å°†å…¶æ’å…¥åˆ°å¤–å±‚å®¹å™¨çš„ =format= å­—ç¬¦ä¸²ä¸­ã€‚

- ç®€åŒ– =t--tags= å‡½æ•°ï¼šâ€‹=t--tags= ä¸­çš„é€»è¾‘å¯ä»¥ç¨å¾®ç®€åŒ–ã€‚spans å˜é‡å·²ç»é€šè¿‡ =t--nw-p= ç¡®ä¿äº†éç©ºï¼Œå› æ­¤å¤–éƒ¨çš„ =if-let*= å¯ä»¥ç›´æ¥ç”¨äºåˆ¤æ–­ class æ˜¯å¦å­˜åœ¨ã€‚

*** ç»“è®º

è¿™æ˜¯ä¸€ä»½å…¸èŒƒæ€§çš„ Emacs Lisp ä»£ç ï¼Œå±•ç¤ºäº†å¦‚ä½•ä¸º Org Mode åˆ›å»ºä¸€ä¸ªåŠŸèƒ½ä¸°å¯Œã€ç”¨æˆ·å‹å¥½çš„å¯¼å‡ºæ‰©å±•ã€‚ä½œè€…æ˜¾ç„¶æ˜¯ä¸€ä½ç»éªŒä¸°å¯Œçš„ Elisp ç¨‹åºå‘˜ã€‚ä¸Šè¿°å»ºè®®ä»…ä¸ºé”¦ä¸Šæ·»èŠ±ï¼Œæ—¨åœ¨è¿›ä¸€æ­¥æå‡ä»£ç çš„æ¸…æ™°åº¦å’Œä¸€è‡´æ€§ã€‚

*** å¯¹ Gemini è¾“å‡ºçš„è¯„ä»·

Gemini è¿™ä¸€æ®µå½©è™¹å±ç»™æˆ‘å¹çš„ğŸ˜‚ã€‚ä¸è¿‡å®ƒæåˆ°çš„æŸäº›åœ°æ–¹æ˜¯å¯ä»¥æ”¹è¿›çš„ã€‚ç›®å‰æ¥çœ‹ï¼Œå®ƒæåˆ°çš„ =<hN>= æ ‡ç­¾ä¸­çš„ ~id="x-%s"~ å±æ€§æœ‰äº›å¤šä½™ï¼Œè¿™ä¸€æ¨¡å¼æ¥è‡ª ReSpecï¼Œä¹Ÿè®¸æˆ‘åº”è¯¥åœ¨åç»­çš„æ”¹è¿›ä¸­å»æ‰å¤šä½™çš„ id å±æ€§ï¼›å¦å¤–å®ƒæåˆ°çš„ =t--build-normal-headline= é€»è¾‘è¿‡äºå¤æ‚çš„é—®é¢˜ä¹Ÿæ˜¯å­˜åœ¨çš„ã€‚

ä¸‹é¢æ€»ç»“ä¸€ä¸‹æˆ‘å‘ç°çš„é—®é¢˜ï¼ŒGemini æ¯•ç«Ÿè¿˜æ˜¯èƒ½åŠ›æœ‰é™ã€‚

** ä½å±‚çº§æ ‡é¢˜çš„ =<ol>=, =<ul>= ç”Ÿæˆé—®é¢˜

è€ƒè™‘ä»¥ä¸‹ Org-mode æ–‡æ¡£å’Œå®ƒçš„ HTML éƒ¨åˆ†å¯¼å‡ºç»“æœï¼š

#+begin_src org
  ,* test1
  ,****** low1
  :PROPERTIES:
  :UNNUMBERED: t
  :END:

  ,****** low2
#+end_src

#+begin_src html
  <ul>
    <li><span id="orguh-1"></span>low1</li>
    <li><span id="orgnh-1.0.0.0.0.1"></span>low2</li>
  </ol>
#+end_src

å¯¹äºæ™®é€šçš„ headlineï¼Œå®ƒä»¬åœ¨ ox-w3ctr ä¸­é»˜è®¤ä½¿ç”¨ =<section>= ä½œä¸ºå¼€å§‹æ ‡ç­¾å’Œé—­åˆæ ‡ç­¾ï¼Œä½†æ˜¯å¯¹äºä½å±‚çº§çš„ headlineï¼Œå®ƒä»¬çš„å¼€é—­æ ‡ç­¾å¹¶ä¸å®Œå…¨ç”±è‡ªå·±å†³å®šï¼Œè¿™æ˜¯ç”±ä½å±‚çº§æ ‡é¢˜çš„å¯¼å‡ºå®ç°å†³å®šçš„ï¼š

#+begin_src elisp
  (concat
   (and (org-export-first-sibling-p headline info)
        (format "<%s>\n" tag))
   "<li>" (format "<span id=\"%s\"></span>" id) text
   (when-let* ((c (t--nw-p contents))) (concat "<br>\n" c))
   "</li>\n"
   (and (org-export-last-sibling-p headline info)
        (format "</%s>\n" tag)))
#+end_src

åœ¨ä¸ä¿®æ”¹å®ç°çš„æƒ…å†µä¸‹ï¼Œè¦æƒ³ç”Ÿæˆæ­£ç¡®çš„ HTMLï¼Œè¿™è¦æ±‚åŒä¸€å±‚çº§çš„ç¬¬ä¸€ä¸ªå’Œæœ€åä¸€ä¸ª headline ä½¿ç”¨ç›¸åŒçš„ =UNNUMBERED= å±æ€§ï¼Œæˆ–è€…ç›´æ¥ç”±å®ƒä»¬çš„çˆ¶èŠ‚ç‚¹æŒ‡å®š =UNNUMBERED= å±æ€§ã€‚æˆ‘çš„å®ç°ç›´æ¥æ¥è‡ª ox-htmlï¼Œä¹Ÿè®¸ä¹‹åèƒ½å¤Ÿæƒ³åˆ°æ›´å¥½çš„å®ç°æ–¹æ³•ï¼Œæˆ–è€…è‡³å°‘èƒ½è¯†åˆ«åˆ°è¿™æ ·çš„é”™è¯¯ã€‚

ä¸è¿‡è¯åˆè¯´å›æ¥ï¼Œç”¨åˆ°ä½å±‚çº§ headline çš„æƒ…å†µå®åœ¨å¤ªå°‘äº†ã€‚

** æ”¯æŒæ— åµŒå¥—çš„ headline å¯¼å‡º

åœ¨ä¸Šé¢æˆ‘ä»¬æåˆ°äº†æŸäº› W3C æ ‡å‡†ä¸­ =<hN>= ä¹‹é—´å¹¶æ²¡æœ‰ =<section>= åµŒå¥—è€Œæ˜¯ç›´æ¥é¡ºåºæ’åˆ—ï¼Œè¿™æ ·çš„å¯¼å‡ºæ–¹å¼ç»™æˆ‘ä¸€ç§å¾ˆå¥½è°ƒè¯•çš„æ„Ÿè§‰ï¼Œè€Œä¸”æ™®é€šçš„ HTML ä¹Ÿç”¨ä¸ä¸Šå¾ˆå¤æ‚çš„åµŒå¥—ã€‚ç›®å‰åœ¨ =org-w3ctr-contianer-element= æŒ‡å®šä¸º =nil= æ—¶ä¼šä½¿ç”¨ =<div>= ä½œä¸º headline çš„å®¹å™¨ï¼Œä¹Ÿè®¸å¯ä»¥è€ƒè™‘åœ¨è¯¥é€‰é¡¹ä¸º =nil= æ—¶ç”Ÿæˆæ— åµŒå¥—çš„ headlineã€‚

å¦å¤–ï¼Œè·å– headline å®¹å™¨çš„ =org-w3ctr--headline-container= åº”è¯¥å¯¹å®ƒè·å–çš„å‚æ•°è¿›è¡Œæ£€æŸ¥æ¥ç¡®ä¿æ ‡ç­¾çš„æ­£ç¡®æ€§ï¼Œä½†ä¹Ÿè®¸è¿™åº”è¯¥æ˜¯ç”±ç”¨æˆ·æ¥ç¡®ä¿ã€‚

** æ ‡é¢˜åºå·ä¸ =<span>= å’Œ =<bdi>=

åœ¨ =org-w3ctr--headline-secno= ä¸­ï¼Œæˆ‘ä½¿ç”¨ =<span>= ä½œä¸ºæ ‡é¢˜åºå·çš„å®¹å™¨ï¼Œä½†åœ¨æŸäº› W3C æŠ€æœ¯æŠ¥å‘Šä¸­ï¼Œä»–ä»¬ä½¿ç”¨çš„æ˜¯ =<bdi>=â€‹ï¼š

#+caption: [[https://www.w3.org/TR/2025/REC-png-3-20250624/#1Scope][REC-png3-20250624]]
#+begin_src html
  <div class="header-wrapper">
    <h2 id="1Scope"><bdi class="secno">2. </bdi>Scope</h2>
    <a class="self-link" href="#1Scope" aria-label="Permalink for Section 2."></a>
  </div>
#+end_src

æ€»æ‰€å‘¨çŸ¥ï¼Œâ€‹=<span>= æ˜¯ä¸€ä¸ªâ€œé€šç”¨â€çš„æ ‡ç­¾ï¼Œå°±åƒ =<div>= ä¸€æ ·ï¼Œé‚£ä¹ˆ =<bdi>= æ˜¯ä»€ä¹ˆä¸œè¥¿ï¼Ÿæ ¹æ® MDN çš„è¯´æ³•ï¼ŒHTML åŒå‘éš”ç¦»å…ƒç´ ï¼ˆBi-Directional Isolationï¼‰å‘Šè¯‰æµè§ˆå™¨çš„åŒå‘ç®—æ³•å°†å…¶åŒ…å«çš„æ–‡æœ¬ä¸å‘¨å›´çš„æ–‡æœ¬éš”ç¦»ï¼Œå½“ç½‘ç«™åŠ¨æ€æ’å…¥ä¸€äº›æ–‡æœ¬ä¸”ä¸çŸ¥é“æ‰€æ’å…¥æ–‡æœ¬çš„æ–¹å‘æ€§æ—¶ï¼Œæ­¤åŠŸèƒ½ç‰¹åˆ«æœ‰ç”¨ã€‚æˆ‘ä»¬å¯ä»¥çœ‹çœ‹å®ƒç»™å‡ºçš„ä¾‹å­çš„æ•ˆæœï¼š

#+begin_src html
  <ul>
    <li><bdi class="name">Evil Steven</bdi>: 1st place</li>
    <li><bdi class="name">FranÃ§ois fatale</bdi>: 2nd place</li>
    <li><span class="name">Ø³Ù…Ø§</span>: 3rd place</li>
    <li><bdi class="name">Ø§Ù„Ø±Ø¬Ù„ Ø§Ù„Ù‚ÙˆÙŠ Ø¥ÙŠØ§Ù†</bdi>: 4th place</li>
    <li><span class="name" dir="auto">Ø³Ù…Ø§</span>: 5th place</li>
  </ul>
#+end_src

#+begin_export html
<ul>
  <li><bdi class="name">Evil Steven</bdi>: 1st place</li>
  <li><bdi class="name">FranÃ§ois fatale</bdi>: 2nd place</li>
  <li><span class="name">Ø³Ù…Ø§</span>: 3rd place</li>
  <li><bdi class="name">Ø§Ù„Ø±Ø¬Ù„ Ø§Ù„Ù‚ÙˆÙŠ Ø¥ÙŠØ§Ù†</bdi>: 4th place</li>
  <li><span class="name" dir="auto">Ø³Ù…Ø§</span>: 5th place</li>
</ul>
#+end_export

æŸäº›è¯­è¨€çš„ä¹¦å†™é¡ºåºæ˜¯ä»å³å‘å·¦ï¼Œæ¯”å¦‚é˜¿æ‹‰ä¼¯è¯­å’Œå¸Œä¼¯æ¥è¯­ï¼Œä½†æ˜¯ä»–ä»¬å¯¹æ•°å­—ä»ç„¶ä½¿ç”¨ä»å·¦åˆ°å³çš„é¡ºåºï¼Œè¿™ä¹Ÿå«æ‰€è°“çš„åŒå‘æ–‡æœ¬ï¼ˆbidirectional textï¼‰ã€‚ä½ å¯ä»¥æ³¨æ„åˆ°åˆ—è¡¨çš„ç¬¬ä¸‰é¡¹çš„æ˜¾ç¤ºæ•ˆæœä¸ HTML æºç å¹¶ä¸ä¸€è‡´ï¼Œè¿™åº”è¯¥èƒ½å¤Ÿä½“ç° =<bdi>= æ ‡ç­¾çš„ä½œç”¨ã€‚

é¡ºå¸¦ä¸€æï¼ŒEmacs çš„ EWW ä¹Ÿæœ‰ç›¸ä¼¼æ˜¾ç¤ºæ•ˆæœï¼ˆä¹Ÿè®¸æ˜¯å› ä¸º Emacs ä½¿ç”¨äº† HarfBuzzï¼Œä¸è¿‡ EWW ç›®å‰ä¸èƒ½è¯†åˆ« =<span>= çš„ =dir= å±æ€§ï¼‰ï¼Œè¿™å¯èƒ½èƒ½å¤Ÿè¯´æ˜å®ƒä¹Ÿé‡‡ç”¨äº†ç±»ä¼¼çš„æ’ç‰ˆç®—æ³•ï¼š

[[./1.jpg]]

ä¹Ÿè®¸æˆ‘åº”è¯¥å¯¹ç”Ÿæˆçš„ secno ä½¿ç”¨ =<bdi>= æ ‡ç­¾ï¼Œä¸è¿‡è¿™å¯¹äºä¸­æ–‡å’Œè‹±æ–‡æ¥è¯´å¹¶ä¸æ˜¯éœ€è¦è€ƒè™‘çš„é—®é¢˜ï¼Œå¦‚æœè¿™ä¸ªåŒ…çœŸçš„æœ‰é˜¿æ‹‰ä¼¯äººä½¿ç”¨çš„è¯ä¹Ÿè®¸ä¼šå‘Šè¯‰æˆ‘ã€‚

** ä½äºæ–‡æ¡£é¡¶éƒ¨çš„æ¬¡çº§æ ‡é¢˜

å¦‚æœæŸä¸ª Org-mode æ–‡æ¡£çš„æœ€å°å±‚çº§ headline ç­‰çº§ä¸º 1ï¼Œä½†æ˜¯åœ¨ç¬¬ä¸€ä¸ª =*= headline çš„ä½ç½®ä¹‹å‰å‡ºç°äº† =**= æˆ– =***= æ ‡é¢˜ï¼Œox-html çš„ç›®å½•ç”Ÿæˆä¼šå‡ºç°ä¸€ç‚¹å°é—®é¢˜ï¼š

#+begin_src org
  ,** WTF
  ,* FIRST
#+end_src

#+begin_src html
  <div id="table-of-contents" role="doc-toc">
    <h2>Table of Contents</h2>
    <div id="text-table-of-contents" role="doc-toc">
      <ul>
        <li><a href="#org6875f0b">0.1. WTF</a></li>
      </ul>
  </li>
  <li><a href="#org36f768e">1. FIRST</a></div>
  </div>
#+end_src

ä½ å¯ä»¥æ³¨æ„åˆ° =WTF= çš„åºå·æ˜¯ =0.1.=â€‹ï¼Œè€Œä¸”åœ¨å®ƒçš„ä¸‹é¢æœ‰ä¸€ä¸ªå¤šä½™çš„ =</ul>= æ ‡ç­¾ï¼Œè¿™å’Œ ox-html çš„ TOC å¯¼å‡ºå®ç°æœ‰å…³ã€‚è€ƒè™‘åˆ°è¿™äº›æ¬¡çº§æ ‡é¢˜çš„ä½ç½®å…¶å®å¹¶ä¸æ˜¯å¾ˆåˆé€‚ï¼Œä¹Ÿè®¸å¯ä»¥åœ¨å¯¼å‡ºç»“æœä¸­å»æ‰å®ƒä»¬ï¼Œä¸è¿‡æ›´åˆç†çš„æ–¹æ³•æ˜¯è°ƒæ•´ TOC ä»£ç æ¥ç”Ÿæˆæ­£ç¡®çš„ç›®å½•ã€‚

* åè®°

æˆ‘åœ¨è¿™ä¸ªæœˆçš„å…­å·å¼€å§‹å†™è¿™ç¯‡åšå®¢ã€‚åŸæœ¬çš„å†…å®¹æ˜¯ä»‹ç» template çš„å¯¼å‡ºï¼Œä½†æ˜¯å…¶ä¸­çš„ TOC ä¼šæ¶‰åŠåˆ° headlineï¼Œå› æ­¤åœ¨å·®ä¸å¤šå†™å®Œçš„æ—¶å€™æˆ‘åˆå›è¿‡å¤´æ¥å…ˆå†™ headlineã€‚å†™å®Œåä¸€çœ‹å·®ä¸å¤šå¤Ÿä¸€ç¯‡ç‹¬ç«‹çš„åšå®¢äº†ï¼Œé‚£ä¸å¦‚å…ˆå‘äº†å†è¯´ã€‚å¦‚æœä¸å‡ºæ„å¤–çš„è¯ï¼Œä¸‹ä¸€ç¯‡æ€»ç®—æ˜¯åˆ°äº† template å’Œ inner-templateã€‚

æ„Ÿè°¢é˜…è¯»ã€‚

#+begin_comment
[[https://www.pixiv.net/artworks/55304797][file:dev/p3.jpg]]

| [[https://www.pixiv.net/artworks/30463131][file:dev/p2.jpg]] | [[https://www.pixiv.net/artworks/41816391][file:dev/p4.jpg]] |
#+end_comment
